#!/usr/bin/env zsh

# Load different types of zsh resources
# Arguments:
#   --script name:  Source a script from $ZSCRIPTS
#   --function name:    Autoload all functions from $ZFUNCTIONS
#   --libs:         Source all library files from $ZLIB
#   --config name:  Get full path to a config file
zc_load() {
  local -A opts
  zparseopts -D -E -A opts \
    -script:=script \
    -function:=function \
    -lib:=lib \
    -config:=config

  [[ ${#opts[@]} != 1 ]] && zc_error "Exactly one parameter must be specified"

  local type=${${(k)opts}[1]}
  local file=${opts[$type]}

  case $type in
    "--script")
      local filepath="$ZSCRIPTS/$file"

      [[ -f $filepath ]] || {
        zc_print --err --trace "Script not found: %s" ${filepath#$ZCUSTOM/} >&2
        return 1
      }

      zc_debug "Sourcing '%s'" ${filepath#$ZCUSTOM/}
      source "$filepath" || return $?
      ;;
    "--function")
      local filepath="$ZFUNCTIONS/$file"

      [[ -f $filepath ]] || {
        zc_print --err "Function not found: %s" ${filepath#$ZCUSTOM/} >&2
        return 1
      }

      zc_debug "Autoloading function '%s'" ${filepath:t}
      autoload -Uz ${filepath}
      ;;
    "--lib")
      local filepath="$ZLIB/$file"

      [[ -f $filepath ]] || {
        zc_print --err "Library not found: %s" ${filepath#$ZCUSTOM/} >&2
        return 1
      }

      zc_debug "Sourcing '%s'" ${filepath#$ZCUSTOM/}
      source $filepath
      ;;

    "--config")
      local filepath="$ZCONFIG/$file"

      [[ -f $filepath ]] || {
        zc_print --err --trace "Config not found: '%s'" ${filepath#$ZCUSTOM/} >&2
        return 1
      }

      echo $filepath
      ;;
  esac
}
